plugins {
    id "java-gradle-plugin"
    id "maven-publish"
    id "org.ajoberstar.grgit" version "1.7.2"
}

group = 'se.inera.intyg.plugin.common'
version = System.properties['buildVersion'] ?: '1.1-SNAPSHOT'

sourceCompatibility = 1.8
targetCompatibility = 1.8

gradlePlugin {
    plugins {
        intygPlugin {
            id = "se.inera.intyg.plugin.common"
            implementationClass = "se.inera.intyg.IntygPlugin"
        }
    }
}

task tagRelease {
    description = 'Tags the current head with the projects version.'
    doLast {
        def grgit = org.ajoberstar.grgit.Grgit.open(creds: new org.ajoberstar.grgit.Credentials(
                System.properties['githubUser'], System.properties['githubPassword']))
        grgit.tag.add {
            name = "v" + version
            message = "Release of ${version}"
        }
        grgit.push(tags: true)
    }
}

publishing {
    repositories {
        maven {
            url "https://build-inera.nordicmedtest.se/nexus/repository/releases/"
            credentials {
                username System.properties['nexusUsername']
                password System.properties['nexusPassword']
            }
        }
    }
}

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.4.11'
    compile 'org.ajoberstar:grgit:1.9.0'
    compile 'net.ltgt.gradle:gradle-errorprone-plugin:0.0.10'
    compile 'gradle.plugin.nl.javadude.gradle.plugins:license-gradle-plugin:0.13.1'
    compile 'org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.5'

    testCompile 'org.spockframework:spock-core:1.0-groovy-2.4'
}

repositories {
    maven { url "https://plugins.gradle.org/m2/" }
    mavenCentral()
}
